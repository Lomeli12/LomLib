buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

def loadProperties(String sourceFileName) {
    def config = new Properties()
    def propFile = new File(sourceFileName)
    if (propFile.canRead()) {
        config.load(new FileInputStream(propFile))
        for (Map.Entry property in config) {
            ext[property.key] = property.value;
        }
    }
}

loadProperties 'build.properties'

repositories {
    maven {
        name = 'codechicken'
        url = "http://chickenbones.net/maven/"
    }
    maven {
        name = 'progwml6'
        url "http://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    compile "codechicken:CodeChickenCore:1.8-$cccVersion:dev"
    compile "codechicken:NotEnoughItems:1.8-$neiVersion:dev"
    compile "codechicken:CodeChickenLib:1.8-$cclVersion:dev"
    deobfCompile "mezz.jei:jei_$mcVersion:$jeiVersion"
}

group = "$group"
archivesBaseName = "$baseName"
version = "$releaseVersion"
sourceCompatibility = "$targetJDK"
targetCompatibility = "$targetJDK"

minecraft {
    version = "$mcVersion-$mcfVersion"
    runDir = "run"
    mappings = "$mcpMap"
}

processResources {
    exclude '**/xcf/**'

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'dependancies.info'
    }
}

jar {
    manifest {
        attributes("FMLAT" : "lomlib_at.cfg")
    }
    appendix = "$mcVersion"
    classifier = "universal"
}

sourceJar {
    appendix = "$mcVersion"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = "$mcVersion"
    classifier = "dev"
    manifest {
        attributes("FMLAT" : "lomlib-deobf_at.cfg")
    }
}

tasks.withType(Zip) { task ->
    task.doLast {
        ant.checksum algorithm: 'MD5', file: it.archivePath
        ant.checksum algorithm: 'SHA1', file: it.archivePath
    }
}

build.dependsOn sourceJar, deobfJar
